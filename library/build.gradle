plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
}

android {
  namespace 'com.mux.stats.sdk.muxstats.media3'
  compileSdk 33

  defaultConfig {
    minSdk 16
    targetSdk 33

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"

    buildConfigField "String", "MEDIA3_VERSION", "\"${media3Version}\""
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds just("android")
  groupIds just("com.mux.stats.sdk.muxstats")
  publicReleaseIf releaseOnTag()

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseOnTag().call()
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }
}

dependencies {
  api 'com.mux.stats.sdk.muxstats:android:dev-releases/v0.7.1-4e1a20a'
  api "androidx.media3:media3-common:${media3Version}"

  // implementation is used so it doesn't pollute customers namespace
  //noinspection GradleDynamicVersion
  implementation "com.mux:utils-kt:dev-releases/v0.7.1-4e1a20a"

  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
