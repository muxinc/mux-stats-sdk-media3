plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
}

android {
  namespace 'com.mux.stats.sdk.muxstats.media3'
  compileSdk 35

  buildFeatures {
    buildConfig = true
  }

  defaultConfig {
    minSdk 19

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  flavorDimensions = ["media3"]
  productFlavors {
    At_latest {
      dimension "media3"
    }
    // This module does not currently need different src sets for different media3 versions.
    // We still need to declare different flavors so we can create version-specific variants
    at_1_0 {
      dimension "media3"
    }
    at_1_1 {
      dimension "media3"
    }
    at_1_2 {
      dimension "media3"
    }
    at_1_3 {
      dimension "media3"
    }
    at_1_4 {
      dimension "media3"
      minSdk 21 // media3 has a minSdk of 21 since v1.4
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds { variant ->
    def media3Variant = variant.productFlavors.find { it.dimension == "media3" }.name
    if (media3Variant.containsIgnoreCase("at_latest")) {
      return "data-media3-custom"
    } else {
      return "data-media3-custom-$media3Variant"
    }
  }
  groupIds just("com.mux.stats.sdk.muxstats")
  publicReleaseIf releaseIfCmdFlag("publicRelease")

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseIfCmdFlag("publicRelease").call()
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }

  dokkaConfig {
    moduleName = "Mux Data SDK for Media3, Base"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
            " Contact support@mux.com"
  }

  pom {
    description "The Mux Data SDK for Google's androidX media3 player"
    inceptionYear = "2022"
    url = "https://github.com/muxinc/mux-stats-sdk-media3"
    organization {
      name = "Mux, Inc"
      url = "https://www.mux.com"
    }
    developers {
      developer {
        email = "support@mux.com"
        name = "The player and sdks team @mux"
        organization = "Mux, inc"
      }
    }
  }
}

dependencies {
  api "com.mux.stats.sdk.muxstats:android:${coreVersion}"

  //noinspection GradleDependency
  at_1_0Api "androidx.media3:media3-common:1.0.0"
  //noinspection GradleDependency
  at_1_1Api "androidx.media3:media3-common:1.1.0"
  //noinspection GradleDependency
  at_1_2Api "androidx.media3:media3-common:1.2.0"
  //noinspection GradleDependency
  at_1_3Api "androidx.media3:media3-common:1.3.0"
  at_1_4Api "androidx.media3:media3-common:1.4.0"
  At_latestApi "androidx.media3:media3-common:1.4.0"

  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.test.ext:junit:1.1.5'
  testImplementation "io.mockk:mockk:1.12.3"
  testImplementation 'org.robolectric:robolectric:4.10.3'

  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
