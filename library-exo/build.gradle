plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
}

android {
  namespace 'com.mux.stats.sdk.muxstats.media3_exo'
  compileSdk 33

  defaultConfig {
    minSdk 16
    targetSdk 33

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  flavorDimensions = ["media3"]
  productFlavors {
    // This module does not currently need different src sets for different media3 versions.
    // We still need to declare different flavors so we can create version-specific variants
    at_1_0 { dimension "media3" }
    at_1_1 { dimension "media3" }
    latest { dimension "media3" }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds { variant ->
    def media3Variant = variant.productFlavors.find { it.dimension == "media3" }.name
    if (media3Variant.containsIgnoreCase("latest")) {
      return "data-media3"
    } else {
      return "data-media3-$media3Variant"
    }
  }
  groupIds just("com.mux.stats.sdk.muxstats")
  publicReleaseIf releaseOnTag()

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseOnTag().call()
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }

  dokkaConfig {
    moduleName = "Mux Data SDK for Media3, ExoPlayer"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
            " Contact support@mux.com"
  }

  pom {
    description "The Mux Data SDK for Google's androidX media3 player"
    inceptionYear = "2022"
    url = "https://github.com/muxinc/mux-stats-sdk-media3"
    organization {
      name = "Mux, Inc"
      url = "https://www.mux.com"
    }
    developers {
      developer {
        email = "support@mux.com"
        name = "The player and sdks team @mux"
        organization = "Mux, inc"
      }
    }
  }
}

dependencies {
  afterEvaluate {
    // The version won't be available when `dependencies` is evaluated
    api "com.mux.stats.sdk.muxstats:data-media3-custom:$project.version"
  }
  // implementation is used so it doesn't pollute customers namespace
  implementation "com.mux:utils-kt:$coreVersion"

//  api "androidx.media3:media3-exoplayer:$media3Version" // TODO: Not all of exo is required
//  compileOnly "androidx.media3:media3-exoplayer-hls:$media3Version"

  //noinspection GradleDependency
  at_1_0Api "androidx.media3:media3-exoplayer:1.0.0"
  //noinspection GradleDependency
  at_1_0CompileOnly "androidx.media3:media3-exoplayer-hls:1.0.0"

  at_1_1Api "androidx.media3:media3-exoplayer:1.1.0"
  at_1_1CompileOnly "androidx.media3:media3-exoplayer-hls:1.1.0"

  latestApi "androidx.media3:media3-exoplayer:1.1.0"
  latestCompileOnly "androidx.media3:media3-exoplayer-hls:1.1.0"

  implementation 'androidx.core:core-ktx:1.10.1'
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'com.google.android.material:material:1.9.0'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

configurations.all {
  resolutionStrategy.dependencySubstitution {
    substitute module("com.mux.stats.sdk.muxstats:data-media3-custom") using project(":library")\
      because "In development, we build from src"
  }
}


