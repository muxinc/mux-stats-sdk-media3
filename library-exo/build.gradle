plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
}

android {
  namespace 'com.mux.stats.sdk.muxstats.media3_exo'
  compileSdk 35

  buildFeatures {
    buildConfig = true
  }

  defaultConfig {
    minSdk 21

    // our deps almost blow the dex limit by themselves, media3 doc/examples all use multidex
    multiDexEnabled true

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  flavorDimensions = ["media3"]
  productFlavors {
    // This module does not currently need different src sets for different media3 versions.
    // We still need to declare different flavors so we can create version-specific variants
    At_latest { dimension "media3" }
    at_1_0 {
     dimension "media3"
      minSdk 19 // minSdk is 19 before 1.4
     }
    at_1_1 {
     dimension "media3"
      minSdk 19 // minSdk is 19 before 1.4
     }
    at_1_2 {
     dimension "media3"
      minSdk 19 // minSdk is 19 before 1.4
     }
    at_1_3 {
     dimension "media3"
      minSdk 19 // minSdk is 19 before 1.4
     }
    at_1_4 {
      dimension "media3"
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds { variant ->
    def media3Variant = variant.productFlavors.find { it.dimension == "media3" }.name
    if (media3Variant.containsIgnoreCase("at_latest")) {
      return "data-media3"
    } else {
      return "data-media3-$media3Variant"
    }
  }
  groupIds just("com.mux.stats.sdk.muxstats")
  publicReleaseIf releaseIfCmdFlag("publicRelease")

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseIfCmdFlag("publicRelease").call()
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }

  dokkaConfig {
    moduleName = "Mux Data SDK for Media3, ExoPlayer"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
            " Contact support@mux.com"
  }

  pom {
    description "The Mux Data SDK for Google's androidX media3 player"
    inceptionYear = "2022"
    url = "https://github.com/muxinc/mux-stats-sdk-media3"
    organization {
      name = "Mux, Inc"
      url = "https://www.mux.com"
    }
    developers {
      developer {
        email = "support@mux.com"
        name = "The player and sdks team @mux"
        organization = "Mux, inc"
      }
    }
  }
}

dependencies {
  debugImplementation project(':library')

  //noinspection GradleDependency
  at_1_0Api "androidx.media3:media3-exoplayer:1.0.0"
  //noinspection GradleDependency
  at_1_0CompileOnly "androidx.media3:media3-exoplayer-hls:1.0.0"

  //noinspection GradleDependency
  at_1_1Api "androidx.media3:media3-exoplayer:1.1.0"
  //noinspection GradleDependency
  at_1_1CompileOnly "androidx.media3:media3-exoplayer-hls:1.1.0"

  //noinspection GradleDependency
  at_1_2Api "androidx.media3:media3-exoplayer:1.2.0"
  //noinspection GradleDependency
  at_1_2CompileOnly "androidx.media3:media3-exoplayer-hls:1.2.0"

  //noinspection GradleDependency
  at_1_3Api "androidx.media3:media3-exoplayer:1.3.0"
  //noinspection GradleDependency
  at_1_3CompileOnly "androidx.media3:media3-exoplayer-hls:1.3.0"

  //noinspection GradleDependency
  at_1_4Api "androidx.media3:media3-exoplayer:1.4.0"
  //noinspection GradleDependency
  at_1_4CompileOnly "androidx.media3:media3-exoplayer-hls:1.4.0"

  //noinspection GradleDependency
  At_latestApi "androidx.media3:media3-exoplayer:1.4.0"
  //noinspection GradleDependency
  At_latestCompileOnly "androidx.media3:media3-exoplayer-hls:1.4.0"

  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.2.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

afterEvaluate {
  // Release builds need mvn coordinates to depend on modules from this project
  //  We only want to add this to release builds, and it has to be the same variant to work.
  //  There's 'at_1_1Api '..' and releaseApi '..', but no at_1_1ReleaseApi '..'
  android.libraryVariants.findAll { it.buildType.name.contains("release") }
          .each { variant ->
            def suffix = variant.productFlavors.get(0).name
            if (suffix.containsIgnoreCase("at_latest")) {
              suffix = "" // 'at_latest' variant has no -at_X_X
            } else {
              suffix = "-$suffix"
            }
            def depNotation =
                    "com.mux.stats.sdk.muxstats:data-media3-custom${suffix}:${project.version}"
            def sourceSet = variant.sourceSets.reverse()[0] // Last src set is most specific
            project.dependencies.add(sourceSet.apiConfigurationName, depNotation)
          }
}

